Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив; 
	МассивНазначений.Добавить("Справочник.Сотрудники"); //Указываем документ к которому делаем внешнюю печ. форму
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма"); 	
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений);
	ПараметрыРегистрации.Вставить("Наименование", "Сведения для бухгалтерии"); //имя под которым обработка будет зарегестрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("БезопасныйРежим", ЛОЖЬ);
	ПараметрыРегистрации.Вставить("Версия", "1.0");       
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, "Сведения для бухгалтерии", "СведенияДляБухгалтерии", "ВызовСерверногоМетода", Истина, "ПечатьMXL");
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	Команды = Новый ТаблицаЗначений;
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));//как будет выглядеть описание печ.формы для пользователя
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка")); //имя макета печ.формы
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка")); //ВызовСерверногоМетода
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
	Возврат Команды;
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;  
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СведенияДляБухгалтерии", "Сведения для бухгалтерии", СформироватьПечатнуюФорму(МассивОбъектов[0], ОбъектыПечати));
	
КонецПроцедуры // Печать()

Функция СформироватьПечатнуюФорму(СсылкаНаДокумент, ОбъектыПечати)Экспорт
	
	//+Дмитрий@, 2024-10-24 07:01:25 LE-052 Добавление типов сертификатов
	ТабличныйДокумент = Новый ТабличныйДокумент;
	МакетОбработки = ПолучитьМакет("Макет");
	
	ОбластьШапка = МакетОбработки.ПолучитьОбласть("Шапка");
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	ОбластьДанные = МакетОбработки.ПолучитьОбласть("Данные"); 
	ОбластьДанные.Параметры.РаботникНаименование  = СсылкаНаДокумент.Наименование;
	ОбластьДанные.Параметры.РаботникДатаРождения  = Формат(СсылкаНаДокумент.ФизическоеЛицо.ДатаРождения,"ДЛФ=DD");  
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
	Запрос.УстановитьПараметр("Сотрудник", СсылкаНаДокумент.ФизическоеЛицо);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ДокументыФизическихЛицСрезПоследних.КемВыдан КАК КемВыдан,
	|	ФизическиеЛицаКонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
	|			,
	|			Физлицо = &Сотрудник
	|				И ВидДокумента.КодМВД = ""21""
	|				И СрокДействия >= &ТекущаяДата) КАК ДокументыФизическихЛицСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|		ПО (ДокументыФизическихЛицСрезПоследних.Физлицо = ФизическиеЛицаКонтактнаяИнформация.Ссылка)
	|			И (ФизическиеЛицаКонтактнаяИнформация.Вид.Наименование = ""Адрес по прописке"")";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл 		
		ОбластьДанные.Параметры.ДокументСерия        = Выборка.Серия;
		ОбластьДанные.Параметры.ДокументНомер        = Выборка.Номер;
		ОбластьДанные.Параметры.ДокументКемВыдан     = Выборка.КемВыдан;
		ОбластьДанные.Параметры.АдресСотрудника      = Выборка.Представление;		
	КонецЦикла;
	
	ОбластьДанные.Параметры.РаботникСНИЛС        = СсылкаНаДокумент.ФизическоеЛицо.СтраховойНомерПФР;
	ОбластьДанные.Параметры.РаботникИНН          = СсылкаНаДокумент.ФизическоеЛицо.ИНН;
	ТабличныйДокумент.Вывести(ОбластьДанные);
	
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	//+/Дмитрий@, 2024-10-24 07:01:25
	
	Возврат ТабличныйДокумент;	
	
КонецФункции

